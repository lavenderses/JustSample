plugins {
  id 'java'
  id 'com.diffplug.spotless' version '6.2.1'
  id 'jacoco'
  id 'checkstyle'
  id 'com.github.spotbugs' version '5.0.5'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'javax.servlet:javax.servlet-api:4.0.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
  testImplementation 'org.easymock:easymock:4.3'

  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
}

spotless {
  format 'misc', {
    target '*.gradle'
    indentWithSpaces()
    trimTrailingWhitespace()
    endWithNewline()
  }
  java {
    importOrder()
    removeUnusedImports()

    def formatVersion = '1.13.0'
    googleJavaFormat(formatVersion)
  }
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
}

checkstyle {
  toolVersion = '8.45'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    xml.required.set(true)
    html.required.set(true)
  }
}
